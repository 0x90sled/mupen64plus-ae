import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.model.application'

model {

    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.3"

        defaultConfig {
            applicationId = "org.mupen64plusae.v3.alpha"
            minSdkVersion.apiLevel = 15
            targetSdkVersion.apiLevel = 23
            versionCode = 37
            versionName = "3.a.0 (debug)"

            buildConfigFields {
                create() {
                    type = "int"
                    name = "VALUE"
                    value = "1"
                }
            }

            ndk {
                moduleName = "mupen64plus"
                stl = 'gnustl_shared'
            }
        }
        buildTypes {
            release {
                minifyEnabled = false
                proguardFiles.add(file("proguard-rules.pro"))
            }
        }

        sources.main {
            java {
                source {
                    srcDirs = ['src/main/java', 'src/main/src']
                }
            }
            jni {
                source {
                    srcDirs = ['src/main/none']
                }
            }
            jniLibs {
                source {
                    srcDirs = ['src/main/libs']
                }
            }
        }
    }
}

// call regular ndk-build(.cmd) script from app directory
task ndkBuild(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'ndk-build.cmd', '--jobs', Runtime.runtime.availableProcessors(),
                'NDK_DEBUG=1', '-C', file('src/main').absolutePath
    } else {
        commandLine 'ndk-build', '--jobs', Runtime.runtime.availableProcessors(),
                'NDK_DEBUG=1', '-C', file('src/main').absolutePath
    }
}

task cleanNative(type: Exec, description: 'Clean JNI object files') {

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine "ndk-build.cmd", '-C', file('src/main/jni').absolutePath, 'clean'
    }else {
        commandLine 'ndk-build', '-C', file('src/main/jni').absolutePath, 'clean'
    }
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ndkBuild
}

clean.dependsOn 'cleanNative'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:preference-v7:23.3.0'
    compile 'com.android.support:preference-v14:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:support-v13:23.3.0'
}